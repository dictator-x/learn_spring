plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

apply from: "$rootDir/gradle/dependencies.gradle"

ext {
    springProject = subprojects.findAll { it.name.startsWith("spring-") }
    springCloudProject = subprojects.findAll { it.name.startsWith("spring-cloud") }
    springCloudAlibabaProject = subprojects.findAll { it.name.startsWith("spring-alibaba") }
}

configure(allprojects) { p ->
    apply plugin: "java"
    sourceCompatibility = 1.8
    repositories {
        mavenCentral()
    }
    dependencies {
        implementation libs.log4j2Api
        implementation libs.log4j2Core
        implementation libs.jodaTime
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testImplementation libs.junitJupiterApi
        testRuntimeOnly libs.junitJupiterEngine
    }
    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

configure(springProject) { p ->
    apply plugin:"org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "application"

    dependencies {
        implementation(project(":common"))
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation libs.jsonWebToken
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'org.springframework.security:spring-security-test'
        runtimeOnly 'mysql:mysql-connector-java'
        testRuntimeOnly 'mysql:mysql-connector-java'
    }
    configurations {
        all {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
    }
}

configure(springCloudProject) { p ->
    dependencyManagement {
        imports {
            mavenBom libs.springCloudDependencies
        }
    }
}

configure(springCloudAlibabaProject) { p ->
    dependencies {
        implementation libs.alibabaNacosSpringStarter
    }
    dependencyManagement {
        imports {
            mavenBom libs.springCloudAlibabaStarter
        }
    }
}
